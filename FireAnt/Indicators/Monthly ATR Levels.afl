_SECTION_BEGIN("ATR Levels");

iATRPeriod = Param("ATRPeriod",2, 1, 10, 1);
dATRLevel = Param("ATRLevel",0.75, 0.25, 2, 0.25); 

TimeFrameSet(inMonthly);    	       // go to monthly chart
monthATR   = ATR(iATRPeriod);   // ATR value
monthClose = C;            // close of last month
TimeFrameRestore();                    // go back to origin timeframe

ATRM_S1 = MonthClose - dATRLevel * monthATR;
ATRM_R1 = MonthClose + dATRLevel * monthATR;

expCloseM = TimeFrameExpand(monthClose, inMonthly, expandLast);
Plot(EMA(expCloseM,iATRPeriod), "ATR Month Middle", colorLightBlue, styleLine  );

expATRM_S1 = TimeFrameExpand(ATRM_S1, inMonthly, expandLast);
S1 = EMA(expATRM_S1,iATRPeriod);
Plot(S1, "ATR Month Support", colorGreen, styleDashed );

expATRM_R1 = TimeFrameExpand(ATRM_R1, inMonthly, expandLast);

R1 = EMA(expATRM_R1,iATRPeriod);
Plot(R1, "ATR Month Resistance", colorRed, styleDashed  );

Buy = Cross(C, S1);
Sell = Cross(R1, C);
Buy = ExRemSpan(Buy, 5);
Sell = ExRemSpan(Sell, 5);
PlotShapes(IIf(Sell, shapeDownArrow , shapeNone), colorRed, 0, H);
PlotShapes(IIf(Buy, shapeUpArrow , shapeNone), colorGreen, 0, L); 

_SECTION_END();